// Persistence of Vision Ray Tracer Scene Description File
// Radiosity scene.pov
// Radiosity sample for POV-Ray version 3.7
// Derived for a sample of Bob Hughes,2001
// by Friedrich A. Lohmueller, Dec 2010
//
//--------------------------------------------------------------------------
#version 3.7;
#default{ finish{ ambient 0.1 diffuse 0.9 }} 
//--------------------------------------------------------------------------
#default{ finish{ ambient 0 diffuse 1 }} 
//------------------------------------------
#include "shapes.inc"
#include "colors.inc"
#include "textures.inc"
//------------------------------------------

global_settings{
  ambient_light 1
  radiosity {
         // pretrace_start 1
         // pretrace_end 1
          error_bound 0.6
          count 50
         // brightness 1
         // gray_threshold 0
         // media on
         // normal on
          recursion_limit 2
       } // --------------
 }// end global_settings
// ----------------------------------------

sky_sphere{ pigment { color rgb <1,1,1.0>*1} }

camera{ location  <0, 14, -20>
        right   x*image_width/image_height  
        rotate 12*y
        look_at   <-1.25,2.8,0>
        angle 33
      }

plane{ <0,1,0>,0 // floor
  texture{
    pigment { color rgb 1 }
    finish {
      ambient 0.0
      diffuse 1
      specular 0.4
      roughness 0.01
    }
  }
}
// -----------------------------------------
 
union {
#for (Cnt,1,3)
  difference {
     box {<-1,-1,-1>,<1,1,1> }
     sphere {0,1.40 inverse  texture{ pigment{ color rgb<1,1,1>*0.5}}  }
     sphere {0,1.25}
     texture{ pigment{ color rgb<1,1,1>}
              finish{ phong 1 diffuse 1-Cnt/10 } 
            }
     translate<0,0,0>
     scale 1+Cnt*0.65 
     translate <0,1+Cnt*0.30,0> 
     rotate Cnt*<0,-22,0>
   }//----------
#end
scale <1,1,1>
translate<0,1,0>
} 
// -----------------------------------------
 

